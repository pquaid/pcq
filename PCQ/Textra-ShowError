/*

Textra-ShowError

This Arexx program displays the errors generated by the PCQ Pascal
compiler using the Textra text editor, version 1.1 or later.  It
should be called as follows:

	Textra-ShowError SourceFile OutputFile ErrorFile

Textra-ShowError reads the errorfile, which must have been
generated by PCQ Pascal in "quiet" mode.  It displays the first
error in the appropriate source file (not necessarily the
SourceFile), then erases the OutputFile and ErrorFile.

This file was designed to be an error handler for the PCQ make
utility.  It might work for other purposes, but I'd be careful if
I were you.

To use this program, you'll need to include the following line in
your configuration file:

	CompilerError rx Textra-ShowError \s \d \e

You'll also need Textra and Arexx.  Arexx is, of course, a
commercial program, and Textra is a shareware text editor written
by:

	Mike Haas
	3867 La Colina Rd.
	El Sobrante, CA 94803
	USA

*/

/* Read the command line parameters */

parse arg SourceFileName OutputFileName ErrorFileName .

options results


/* Get the first error */

if open(logfile, ErrorFileName, 'R') then do

    if eof(logfile) then do   /* There were no errors */
	FileName = SourceFileName
	LineNo = 1
	ColumnNo = 1
	ErrorText = "No Errors"
    end; else do
        ErrorString = readln(logfile)

        /* This first test is just a reminder, in case you want to */
        /* process all errors.  "Abort" would never come first.    */

        if ErrorString = "Compilation Aborted" then do
            address command 'delete >nil:' outputfile
            'okay1 Compilation Aborted'
            exit
        end

        if ErrorString ~= "" then do
            parse var ErrorString '"' FileName '" At ' LineNo,
                                       ',' ColumnNo ' :' ErrorText
	end; else do
	    FileName = SourceFileName
	    LineNo = 1
	    ColumnNo = 1
	    ErrorText = "No Errors"
	end
    end
    close(logfile)
end; else do
    say 'Could not open error file'
    exit
end

/* Load Textra */

address command 'RUN Textra ' || FileName
do until show(Ports,'TEXTRA')
    address command wait 1
end

address 'TEXTRA'

'GOTOXY' 0 LineNo-1         /* Move to the correct line */

do for ColumnNo
    'RIGHT 1'
end

Notify "Error:" || ErrorText

/* Delete the temporary files */

address command 'delete >Nil:' ErrorFileName
address command 'delete >Nil:' OutputFileName

